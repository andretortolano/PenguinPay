import deps.Deps
import deps.LocalDeps
import deps.TestDeps

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.31"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion GradleConfig.targetSdk

                defaultConfig {
                    minSdkVersion GradleConfig.minSdk
                    targetSdkVersion GradleConfig.targetSdk

                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_1_8.toString()
                }

                buildFeatures {
                    viewBinding true
                }
            }

            dependencies {
                implementation(
                        Deps.kotlin,
                        Deps.kotlinCoroutines,
                        Deps.kotlinCoroutinesAndroid,
                        Deps.androidxCompat,
                        Deps.koinAndroid,
                )

                testImplementation(
                        TestDeps.junit,
                        TestDeps.googleTruth,
                        TestDeps.mockk,
                        TestDeps.coroutines,
                        TestDeps.androidxCore
                )
            }
        }
    }
}

